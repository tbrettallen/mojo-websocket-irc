#!/usr/bin/perl

# Copyright (c) 2010, David Davis - http://xant.us/

# WebSocket Example 2 - irc client
# Powered by Mojo - http://mojolicious.org/ - http://github.com/kraih/mojo
# and web-socket-js - http://github.com/gimite/web-socket-js

use FindBin;
die "You need to run 'git submodule update --init' to fetch the example requirements\n"
    unless -d "$FindBin::Bin/../mojo/lib";

use lib "$FindBin::Bin/../mojo/lib";

use Mojo::IOLoop;
use Mojolicious::Lite;

use strict;
use warnings;
use bytes;

# The loop
my $loop = Mojo::IOLoop->singleton;

# Connection list
my $c = {};

@ARGV = qw( daemon ) unless @ARGV;

my $irc_server = 'irc.hackthissite.org';
my $irc_port = 6667;

websocket '/' => sub {
    my $client = shift;

    app->log->debug( "client connected, connecting to $irc_server" );

#    $client->send_message( "Connecting to $irc_server" );

    my $con = $c->{"$client"} = {
        buffer => ''
    };

    $client->on(finish => sub {
        app->log->debug( "client finished, dropping conneciton to irc server" );
        $loop->drop( $con->{irc} )
            if $con->{irc};

        delete $c->{"$client"};
        return;
    });

    $client->on(message => sub {
        $con->{buffer} ||= '';
        $con->{buffer} .= $_[1]; # chunk

        $loop->write( $con->{irc} => delete $con->{buffer} )
            if $con->{irc} && length $con->{buffer};
    });

    my $sockClient = Mojo::IOLoop::Client->new;
    
    $sockClient->on(connect => sub {
        app->log->debug( "Connected to $irc_server" );
        $client->send( "Connected to $irc_server" );

        $con->{irc} = delete $con->{_irc};
        $loop->write( $con->{irc} => delete $con->{buffer} )
            if length $con->{buffer};
    });
        
    $sockClient->on(read => sub {
            $client->send( $_[2] ); # chunk
    });
        
    $sockClient->on(error => sub {
            app->log->debug( "Disconnected from $irc_server (connection error)" );
            $loop->drop( $client->tx->connection );
    });
        
    $sockClient->on(close => sub {
            app->log->debug( "Disconnected from $irc_server (hangup)" );
            $loop->drop( $client->tx->connection );
    });
    
    $con->{_irc} = $sockClient->connect(
        address => $irc_server,
        port    => $irc_port
    );

    #$loop->connection_timeout( $con->{_irc} => 600 );

    return;
};

get '/' => 'index';

# see script/flash-policy-server
print "Chrome users: connect to 127.0.0.1 instead of localhost\n";
print "Remember, you need to also run script/flash-policy-server as root for this to work on non WebSocket browsers...\n";

app->start;

1;

__DATA__

@@ index.html.ep
% my $url = $self->req->url->to_abs->scheme( $self->req->is_secure ? 'wss' : 'ws' )->path( '/' );
<!doctype html>
<html>
<head>
    <title>Mojo Websocket Demo - ExtJS IRC Client</title>

    <link rel="stylesheet" type="text/css" href="js/ext-3.2-rc/resources/css/ext-all.css" />
    <script type="text/javascript" src="js/ext-3.2-rc/adapter/ext/ext-base-debug.js"></script>
    <script type="text/javascript" src="js/ext-3.2-rc/ext-all-debug.js"></script>


    <script type="text/javascript">
        // only load the flash fallback when needed
        if ( !( 'WebSocket' in window ) ) {
            document.write([
                '<scr'+'ipt type="text/javascript" src="web-socket-js/swfobject.js"></scr'+'ipt>',
                '<scr'+'ipt type="text/javascript" src="web-socket-js/FABridge.js"></scr'+'ipt>',
                '<scr'+'ipt type="text/javascript" src="web-socket-js/web_socket.js"></scr'+'ipt>'
            ].join(''));
        }
    </script>
    <script type="text/javascript">
        if ( WebSocket.__initialize ) {
            // Set URL of your WebSocketMain.swf here:
            WebSocket.__swfLocation = 'web-socket-js/WebSocketMain.swf';
        }
        Ext.BLANK_IMAGE_URL = 'js/ext-3.2-rc/resources/images/default/s.gif';
    </script>

    <script type="text/javascript" src="js/filter/Filter.js"></script>
    <script type="text/javascript" src="js/filter/Line.js"></script>
    <script type="text/javascript" src="js/filter/IRC.js"></script>
    <link rel="stylesheet" type="text/css" href="css/irc.css" />
    <script type="text/javascript" src="js/irc-client.js"></script>
    <script type="text/javascript">
        Ext.onReady(function() {
            var win = Ext.create({
                title: 'Mojo WebSocket Demo - ExtJS IRC Client',
                xtype: 'irc-client',
                renderTo: 'irc-panel',
                width: 800,
                height: 400,
                x: 10,
                y: 10,
                wsUrl: '<%= $url  %>'
            });
            win.show();
        });
    </script>
</head>
<body>
    <h2>Mojo WebSocket Demo - ExtJS IRC Client</h2>
    <div id="irc-panel"></div>
</body>
</html>

